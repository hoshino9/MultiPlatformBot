buildscript {
    ext.kotlin_version = '1.3.31'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath 'com.qihoo360.replugin:replugin-plugin-gradle:2.2.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"


        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'replugin-plugin-gradle'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.plugin"//插件包名，应保证唯一性
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java.srcDirs 'src/main/gen'
        }
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation(project(":common:multi"))

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.qihoo360.replugin:replugin-plugin-lib:2.2.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

task genAntlr(type: JavaExec) {
    def packageName = "org.hoshino9.robot.parser.internal"

    classpath = files("../../common/multi/libs/antlr-4.7.2-complete.jar")
    main = 'org.antlr.v4.Tool'
    args("./src/main/antlr/Message.g4",
            "-package", packageName,
            "-o", "./src/main/gen/${packageName.replace('.', '/')}")
}

assemble.dependsOn(genAntlr)